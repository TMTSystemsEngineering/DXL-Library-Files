/* 
 Populate the VA Links attribute in the DRD modules - for TMT
 March 2017

 Version: 3

 Description: 	This Layout DXL script populates the VA Links attribute in DRD modules.
		The VA Links attribute is filled with 1 line of text per linked VA.
		Each line contains 4 fields: VA ID, VA Name, VA Method, VA Milestone
		Example of the desired VA Links contents:
			[VA-2-ENC-8] List of Standards [D] [SM FDR]

 Assumptions:	The script requires the following conditions for proper function:
		1) The verification link module is: "/TMT Requirements/Links/Verification"
		2) The user must have read access to the VAM modules
		   The modules don't need to be open. 
		   If not open, then they are opened in the background - but not displayed.
  
*/
pragma runLim, 0

string LinkModuleVAs = "/TMT Requirements/Links/Verification"

string NewText = ""
bool firstLink = true
Link inLink
Object sourceObj
LinkRef lr
ModName_ sourceMod

string VA_ID = ""
string VA_Name = ""
string VA_Method = ""
string VA_Milestone = ""


// Check all inlinks: if the source module is not open, then load it

for lr in all(obj<-LinkModuleVAs) do {
   sourceMod = module (sourceVersion lr)
   if (!null sourceMod) {
      if ((!isDeleted sourceMod) && (null data(sourceVersion lr))) {
         load((sourceVersion lr),false)
      }
   }
}



// Loop over all incoming links from the verification link module to this obj

for inLink in obj <- LinkModuleVAs do	
{

// Get the source for this link

   sourceObj = source inLink			// get the source object number


// Get the string values from the source module
   
   VA_ID = identifier(sourceObj)
   VA_Name = sourceObj."Object Text"
   VA_Method = sourceObj."VA Method"
   VA_Milestone = sourceObj."VA Milestone"

// Add this link info to the end of the NewText string

   if ( firstLink ) 
   {
      NewText = "[" VA_ID "] " VA_Name " [" VA_Method "] [" VA_Milestone "]"
      firstLink = false

   } else {
   
      NewText = NewText "\n[" VA_ID "] " VA_Name " [" VA_Method "] [" VA_Milestone "]"

   }

}

display NewText ""
