/* 
 Find Duplicate IDs - for TMT
 By Karim Kashani - JPL 2014

 Description: This script locates duplicate IDs within a module, using the TMT attribute entitled "Requirement ID"
 Instructions: Open the module, load the script and run. It will print out to the DXL Output window at the bottom of 
		the screen, showing the "Duplicate Requirement ID" and each DOORS object (Object Identifier) containing
		at least one instance of a duplicate Requirement ID.

               Version History:
               v1.0 - Written and tested against L1 OAD module in JPL's test TMT project space.
               v1.1 - Modified by Mitchell Troy
                      -tmtReqField is now a variable so it's easier for the user to change
                       Added several print statements to state number of duplicate id's found
                         and number searched
                        Currently only works if All requirements are 4 integers.

*/
string tmtReqField = "Requirement ID"
Object o 
Object filteredIDs
string currentid = ""
string id2Compare
string doorsID = ""
string parsedID = ""
Filter findIDs 
int idCount = 0
int dupIdCount = 0
int numIdSearched = 0
Regexp idpattern = regexp "-[0-9][0-9][0-9][0-9]"  //at least four contiguous numbers in ID is consistent across all modules

for o in current Module do {        //for each object in DOORS module

  id2Compare = o.tmtReqField   //return entire value of Requirement ID attribute
  
  if (idpattern id2Compare){        //if find a valid ID 
      numIdSearched++
      findIDs = contains (attribute tmtReqField, id2Compare, false) // assign to a filter
      set findIDs
      filtering on         //filter on all objects containing the id2Compare         
      idCount = 0
      for filteredIDs in current Module do {   //Tally the number of filtered objects
            
            idCount++
      }
      if (idCount > 1){         //if there's more than one

         print "Duplicate Requirement ID: " id2Compare ":" "\n" //print out the duplicate Requirement ID
		 dupIdCount++
         for filteredIDs in current Module do{
            print "Object ID: " identifier(filteredIDs) "\n"    //print out each DOORS object/requirement with
								    //the duplicate ID.
         }
      
            print "*****""\n"
      }
      filtering off
  }

}
dupIdCount = dupIdCount/2 //each duplicate get's counted twice 
print "Number of duplicate Requirement ID's found: " dupIdCount "\n"
print "Number of Requirement ID's searched: "numIdSearched "\n"
